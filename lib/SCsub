Import('*')

import os

glad_include = ""
glad_src = ""

if env['flavor'] == 'debug':
	glad_include = "#lib/glad/debug-include"
	glad_src = "glad/glad-debug.c"
else:
	glad_include = "#lib/glad/include"
	glad_src = "glad/glad.c"

#add the include directories for each libray to the CPPPATH
env.Append(CPPPATH = ['#lib/lua-5.3.3', '#lib/stb', '#lib/glfw-3.2.1/include', glad_include, '#lib/glm', '#lib/enet-1.3.13/include'])
env.Append(LIBPATH = [os.path.join('#$BINDIR', 'lib')])

module_targets = {}
#lua is very nice to build
module_targets['lua'] = env.Library(target = 'liblua.a', source = Glob('lua-5.3.3/lua/*.c'), CCFLAGS = env['CCFLAGS'] + ['-DLUA_COMPAT_5_2'])

#so is glad
module_targets['glad'] = env.Library(target = 'libglad.a', source = glad_src)

#glfw config (only compiling wayland files for now will worry about other platforms later)
glfw_sources_x11 = ['context.c', 'init.c', 'input.c', 'linux_joystick.c', 'monitor.c', 'posix_time.c', 'posix_tls.c', 'vulkan.c', 'window.c', 'glx_context.c', 'egl_context.c',
'x11_init.c', 'x11_monitor.c', 'x11_window.c', 'xkb_unicode.c']
glfw_sources_in = ['context.c', 'init.c', 'input.c', 'linux_joystick.c', 'monitor.c', 'posix_time.c', 'posix_tls.c', 'vulkan.c', 'window.c', 'egl_context.c',
'wl_init.c', 'wl_monitor.c', 'wl_window.c', 'xkb_unicode.c', 'wayland-relative-pointer-unstable-v1-protocol.c', 'wayland-pointer-constraints-unstable-v1-protocol.c']
glfw_sources = ['glfw-3.2.1/src/' + name for name in glfw_sources_x11]

#standard c flags plus the macro to tell glfw that we will be using wayland
glfw_flags = ['-D_GLFW_X11']
module_targets['glfw'] = env.Library(target = 'libglfw3.a', source = glfw_sources, CCFLAGS = env['CCFLAGS'] + glfw_flags)

#and finally enet
enet_sources_in = ['callbacks.c', 'compress.c', 'host.c', 'list.c', 'packet.c', 'peer.c', 'protocol.c', 'unix.c']
enet_sources = ['enet-1.3.13/' + name for name in enet_sources_in]
enet_flags = ['-DPACKAGE_NAME=\"libenet\"',  '-DPACKAGE_TARNAME=\"libenet\"', '-DPACKAGE_VERSION=\"1.3.13\"', '-DPACKAGE_STRING=\"libenet\ 1.3.13\"', '-DPACKAGE_BUGREPORT=\"\"', '-DPACKAGE_URL=\"\"', '-DPACKAGE=\"libenet\"', '-DVERSION=\"1.3.13\"', '-DSTDC_HEADERS=1', '-DHAVE_SYS_TYPES_H=1', '-DHAVE_SYS_STAT_H=1', '-DHAVE_STDLIB_H=1', '-DHAVE_STRING_H=1', '-DHAVE_MEMORY_H=1', '-DHAVE_STRINGS_H=1', '-DHAVE_INTTYPES_H=1', '-DHAVE_STDINT_H=1', '-DHAVE_UNISTD_H=1', '-DHAVE_DLFCN_H=1', '-DLT_OBJDIR=\".libs/\"', '-DHAS_GETHOSTBYADDR_R=1', '-DHAS_GETHOSTBYNAME_R=1', '-DHAS_POLL=1', '-DHAS_FCNTL=1', '-DHAS_INET_PTON=1', '-DHAS_INET_NTOP=1', '-DHAS_MSGHDR_FLAGS=1', '-DHAS_SOCKLEN_T=1']

module_targets['enet'] = env.Library(target = 'libenet.a', source = enet_sources, CCFLAGS = env['CCFLAGS'] + enet_flags)

Return('module_targets')
